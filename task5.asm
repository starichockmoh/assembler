.model tiny
.386  
.data                   ;сегмент данных
    mode db (?)         ;номер режима, начальное значение не определено (равно ?)
.code                   ;сегмент кода
org 100h 
start: 
    mov ax,0b900h       ;используя сегментный регистр ES,   
    mov es,ax           ;организовать запись данных в видеопамять по адресу В900h:0000h (страница 1)
    call B10MODE        
    call B10DISPLAY
    mov ah,10h          ;запрос на получение символа с клавиатуры
    int 16h
    mov ah,00h          ;запрос на установку исходного видеорежима
    mov al, mode 
    int 10h 
    
    mov ax,4C00h        ;завершение программы
    int 21h

B10MODE proc            ;получение и сохранение текущего режима, установка нового режима и страницу,
    mov ah,0Fh          ;получаем текущий режим в регистре AL: в регистр AH заносим значение 0Fh после чего вызывается прерывание 10h.
    int 10h
    mov mode,al         ;сохраняем номер текущего режима: заносим в mode содержимое регистра AL.
    mov ah,00h          ;переводим экран в текстовый режим с номером 03
    mov al,03
    int 10h
    mov ah,05h          ;устанавливаем страницу 1
    mov al,01h
    int 10h
    ret
B10MODE endp


B10DISPLAY proc
    pusha
    mov al,41h          ;помещаем в al код начального символа 'A'
    mov ah,0Ah          ;пощаем в ah значение атрибута (черный фон и цвет символа - в начале это зеленый)
    mov di,830          ;адрес начального смещения в видеопамяти (5:15) 
    mov cx,5            ;кол-во столбцов
    external:           ;внешний цикл - по строкам
        push cx         ;сохраняем текущий индекс цикла
        mov cx,10       ;кол-во символов в строке (кол-во столбцов)
        internal:       ;внутренний цикл - по столбцам
            mov es:word ptr[di], ax ;заносим символ (AL) в первый байт видеопамяти и атрибут (AL) во второй
            add di,2    ;переходим к следующей позиции в строке
        loop internal    
        add di,140      ;добавляем до начала новой строки
        pop cx          ;воостановаливаем CX
        inc ah          ;следующий атрибут (цвет)
        inc al          ;следующий символ
    loop external
    popa
    ret
B10DISPLAY endp

end start                ;конец программы